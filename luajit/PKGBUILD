# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has a "rolling release" where you should follow git HEAD
_commit=7a526c78949b6cd9d6b7cc83c3ad908df0dc6e1c
# The patch version is the timestamp of the above git commit, obtain via `git show -s --format=%ct`
_ct=1658991363
pkgver="2.1.${_ct}"
pkgrel=11
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('loong64' 'x86_64')
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://github.com/sophie-zhao/LuaJIT/archive/${_commit}.tar.gz")
md5sums=('39069473555de4c3da74f856e8def899')
sha256sums=('8f09bdce136b020ad9a50501b4fa64128309cb633fa2888e51210b38652ab85a')
b2sums=('baabf7fb403a5e143a7019c047dde5d3f0d03f03265d8eb1939e6b0e9cc8268b5cf98adc4a8463a6fa0a519054027696f6ddc7ee1fb3150d10f0d53d4c62957e')

build() {
  cd "LuaJIT-${_commit}"

  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

check() {
  cd "LuaJIT-${_commit}"

  # Make sure that _ct was updated
  test "${_ct}" == "$(cat .relver)"
}

package() {
  cd "LuaJIT-${_commit}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
